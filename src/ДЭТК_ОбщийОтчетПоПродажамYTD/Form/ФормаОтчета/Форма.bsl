
//формирует расшифровку основного отчета по документам
&НаСервере
Процедура СформироватьРасшифровкуНаСервере()
	Об = РеквизитФормыВЗначение("Отчет");
	Макет = Об.ПолучитьМакет("Расшифровка");
	
	ТабРезультат.Очистить();
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Организация=РОрганизация;
	Область.Параметры.ПериодОтчета = "Период отчета: "+Формат(РДатаНач, "ДФ=дд.ММ.гггг") + " - " + Формат(РДатаКон, "ДФ=дд.ММ.гггг");
	ТабРезультат.Вывести(Область);
	
	
	//выполняем запрос, который все продажи собирает в одну таблицу
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(РДатаНач));
	Запрос.УстановитьПараметр("ЕстьДатаНач", ЗначениеЗаполнено(РДатаНач));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(РДатаКон));
	Запрос.УстановитьПараметр("ЕстьДатаКон", ЗначениеЗаполнено(РДатаКон));
	
	Запрос.УстановитьПараметр("ЕстьОрганизация", ЗначениеЗаполнено(РОрганизация));
	Запрос.УстановитьПараметр("Организация", РОрганизация);
	
	Запрос.УстановитьПараметр("ЕстьКонтрагент", ЗначениеЗаполнено(РКонтрагент));
	Запрос.УстановитьПараметр("Контрагент", РКонтрагент);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Рег.Период КАК Период,
	|	Рег.Регистратор КАК Документ,
	|	Рег.Организация КАК Организация,
	|	Рег.Контрагент КАК Контрагент,
	|	СУММА(Рег.Себестоимость) КАК Себестоимость,
	|	СУММА(Рег.ОбъемПродаж) КАК ОбъемПродаж
	|ИЗ
	|	РегистрНакопления.ДЭТК_Продажи КАК Рег
	|ГДЕ
	|	1 = 1
	|	И ВЫБОР
	|			КОГДА &ЕстьДатаНач
	|				ТОГДА Рег.Период >= &ДатаНач
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ЕстьДатаКон
	|				ТОГДА Рег.Период <= &ДатаКон
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ЕстьОрганизация
	|				ТОГДА Рег.Организация = &Организация
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ЕстьКонтрагент
	|				ТОГДА Рег.Контрагент = &Контрагент
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	Рег.Организация,
	|	Рег.Контрагент,
	|	Рег.Регистратор,
	|	Рег.Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	Рег.Регистратор.Дата,
	|	Рег.Регистратор.Номер
	|ИТОГИ
	|	СУММА(Себестоимость),
	|	СУММА(ОбъемПродаж)
	|ПО
	|	Контрагент";
	
	ТЗИтоги = Новый ТаблицаЗначений;
	ТЗИтоги.Колонки.Добавить("Контрагент");
	ТЗИтоги.Колонки.Добавить("Себестоимость");
	ТЗИтоги.Колонки.Добавить("ОбъемПродаж");
	ТЗИтоги.Индексы.Добавить("Контрагент");
	
	Рез = Запрос.Выполнить();	
	Выборка = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
	
		Область = Макет.ПолучитьОбласть("Строка");
		Область.Параметры.Контрагент = Выборка.Контрагент;
		
		Область.Параметры.Себестоимость	= Выборка.Себестоимость;
		Область.Параметры.ОбъемПродаж	= Выборка.ОбъемПродаж;
		Попытка
			Область.Параметры.Рентабельность = ?(Выборка.Себестоимость=0, 0, (Выборка.ОбъемПродаж-Выборка.Себестоимость)/Выборка.Себестоимость*100);
		Исключение
		КонецПопытки;
		
		НовСтр = ТЗИтоги.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
		
		ТабРезультат.Вывести(Область);
		
		//уровень документов
		Выборка2 = Выборка.Выбрать();
		
		сч=1;
		Пока Выборка2.Следующий() Цикл
			
			Область							= Макет.ПолучитьОбласть("СтрокаДок");
			Область.Параметры.Документ		= Выборка2.Документ;
			Область.Параметры.ДатаДок		= Формат(Выборка2.Период, "ДФ=дд.ММ.гггг");
			Область.Параметры.Себестоимость	= Выборка2.Себестоимость;
			Область.Параметры.ОбъемПродаж	= Выборка2.ОбъемПродаж;
			Попытка
				Область.Параметры.Рентабельность = ?(Выборка2.Себестоимость=0, 0, (Выборка2.ОбъемПродаж-Выборка2.Себестоимость)/Выборка2.Себестоимость*100);
			Исключение
			КонецПопытки;
			
			ТабРезультат.Вывести(Область);
	//		
	//		СтрТотал = ТЗПериодов.Найти(Выборка2.Период, "Период");
	//		
	//		Попытка
	//			СтрТотал.ОбъемПродажТотал = СтрТотал.ОбъемПродажТотал + Выборка2.ОбъемПродаж;
	//		Исключение
	//		КонецПопытки;
	//		
	//		Попытка
	//			СтрТотал.СебестоимостьТотал = СтрТотал.СебестоимостьТотал + Выборка2.Себестоимость;
	//		Исключение
	//		КонецПопытки;
	//		
	//		//для расшифровки
	//		//Область.Расшифровка
	//		
	//		
	//		сч=сч+1;
	//		
		КонецЦикла;
	//	
	//	
	КонецЦикла; 
	//
	//
	////подвал
	//
	Область = Макет.ПолучитьОбласть("Подвал");
	
	сс=ТЗИтоги.Итог("Себестоимость");
	оп=ТЗИтоги.Итог("ОбъемПродаж");
	Область.Параметры.Себестоимость	= сс;
	Область.Параметры.ОбъемПродаж	= оп;
			
	Попытка
		Область.Параметры.Рентабельность = ?(сс=0, 0, (оп-сс)/сс*100);
	Исключение
	КонецПопытки;
	ТабРезультат.Вывести(Область);
	
	//завершение
	ТабРезультат.Защита = Истина;
	
КонецПроцедуры

//формирует основной отчет
&НаСервере
Процедура СформироватьОтчетНаСервере()
	Об = РеквизитФормыВЗначение("Отчет");
	Макет = Об.ПолучитьМакет("Макет");
	
	ТабРезультат.Очистить();
	
	//шапка отчета
	Область = Макет.ПолучитьОбласть("Шапка|Начало");
	Область.Параметры.Организация=Организация;
	Область.Параметры.ДатаОтчета = "На дату: "+Формат(НаДату, "ДФ=дд.ММ.гггг");
	ТабРезультат.Вывести(Область);
	
	//а это заголовок таблицы
	Область = Макет.ПолучитьОбласть("Заголовок|Начало");
	ТабРезультат.Вывести(Область);
	
	
	//выполняем запрос, который все продажи собирает в одну таблицу
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДатаНач", НачалоГода(НаДату));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(НаДату));
	
	Запрос.УстановитьПараметр("ЕстьОрганизация", ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.УстановитьПараметр("ЕстьКонтрагент", ЗначениеЗаполнено(Контрагент));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Рег.Период КАК Период,
	|	Рег.Организация КАК Организация,
	|	Рег.Контрагент КАК Контрагент,
	|	Рег.СебестоимостьОборот КАК Себестоимость,
	|	Рег.ОбъемПродажОборот КАК ОбъемПродаж
	|ПОМЕСТИТЬ ВТПродажи
	|ИЗ
	|	РегистрНакопления.ДЭТК_Продажи.Обороты(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Месяц,
	|			ВЫБОР
	|					КОГДА &ЕстьОрганизация
	|						ТОГДА Организация = &Организация
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|				И ВЫБОР
	|					КОГДА &ЕстьКонтрагент
	|						ТОГДА Контрагент = &Контрагент
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ) КАК Рег";
	
	Запрос.Выполнить();
	
	//выбрать из таблицы продаж месяцы, чтобы построить шапку макета
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ.Период КАК Период
	|ИЗ
	|	ВТПродажи КАК ВТ
	|УПОРЯДОЧИТЬ ПО
	|	ВТ.Период";
	
	Рез = Запрос.Выполнить();
	
	//выгрузим этот запрос в таблицу, она потом пригодится
	ТЗПериодов = Рез.Выгрузить();
	ТЗПериодов.Индексы.Добавить("Период");
	
	//поля для накопления итогов для подвала
	ТЗПериодов.Колонки.Добавить("ОбъемПродажТотал", Новый ОписаниеТипов("Число"));
	ТЗПериодов.Колонки.Добавить("СебестоимостьТотал", Новый ОписаниеТипов("Число"));
	
	//вывод в отчет шапки. все колонки с месяцами, которые есть в запросе
	сч = 1;
	Для Каждого СтрокаПериод Из ТЗПериодов Цикл
		
		Область = Макет.ПолучитьОбласть("Заголовок|Месяц");
		Область.Параметры.Месяц = Формат(СтрокаПериод.Период, "ДФ=ММММ");
		
		//расшифровка
		Область.Параметры.РасшифровкаМесяца = Новый Структура("Организация, Контрагент, ДатаНач, ДатаКон",
			Организация, Неопределено, НачалоМесяца(СтрокаПериод.Период), КонецМесяца(СтрокаПериод.Период));
		
		Область.Область("R1C1:R2C3").ЦветФона = ФормулаЦвета(сч);
		
			
			
		ТабРезультат.Присоединить(Область);
			
		сч=сч+1;
	КонецЦикла;
	
	
	//выбрать контрагентов и обороты по ним. продажи будут присоединяться к таблице периодов, чтобы 
	//в случае отсутствия оборотов в запросе все же был этот период
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТ.Период КАК Период,
	|	ВТ.Контрагент КАК Контрагент,
	|	СУММА(ВТ.Себестоимость) КАК Себестоимость,
	|	СУММА(ВТ.ОбъемПродаж) КАК ОбъемПродаж
	|ИЗ
	|	ВТПродажи КАК ВТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ.Период,
	|	ВТ.Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент,
	|	Период
	|ИТОГИ
	|	СУММА(Себестоимость),
	|	СУММА(ОбъемПродаж)
	|ПО
	|	Контрагент, 
	|	Период ПЕРИОДАМИ (МЕСЯЦ, &ДатаНач, &ДатаКон)";
	
	
	Рез = Запрос.Выполнить();	
	
	//ТЗЗЗЗ = Рез.Выгрузить();
	//
	//ДЗЗЗЗ = Рез.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	
	Выборка = Рез.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		//обход контрагентов
		Область = Макет.ПолучитьОбласть("Строка|Начало");
		Область.Параметры.Контрагент = Выборка.Контрагент;
		
		//расшифровка
		Область.Параметры.РасшифровкаКонтрагента = Новый Структура("Организация, Контрагент, ДатаНач, ДатаКон",
			Организация, Выборка.Контрагент, Неопределено, Неопределено);
		
		ТабРезультат.Вывести(Область);
		
		
		//"ВСЕ" - означет, что нужно выбрать все периоды, т.е. в нашем случае, с января по декабрь.
		//но нам это не надо, т.к. незачем выводить те периоды, когда совсем не было оборотов ни у одного контрагента.
		//Чтобы убрать лишние периоды, будем проверять наличие периода в таблице ТЗПериодов , если там такого периода нет,
		//не будем его выводить здесь
		//возможные проблемы. пример.
		//если будут обороты за февраль, апрель, июнь, то отчет не выведет март и май, т.к. их не будет в таблице ТЗПериодов.
		//это не смертельно, но некрасиво
		
		Выборка2 = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период", "ВСЕ");
		
		сч=1;
		Пока Выборка2.Следующий() Цикл
			
			//если в ТЗПериодов такого периода нет, то не надо выводить данные по нему, в т.ч. пустые
			Если ТЗПериодов.Найти(Выборка2.Период, "Период")=Неопределено Тогда
				Продолжить;				
			КонецЕсли;
			
			Область = Макет.ПолучитьОбласть("Строка|Месяц");
			Область.Параметры.ОбъемПродаж = Выборка2.ОбъемПродаж;
			Область.Параметры.Себестоимость = Выборка2.Себестоимость;
			Попытка
				Область.Параметры.Рентабельность = ?(Выборка2.Себестоимость=0, 0, (Выборка2.ОбъемПродаж-Выборка2.Себестоимость)/Выборка2.Себестоимость*100);
			Исключение
			КонецПопытки;
			
			//расшифровка
			Область.Параметры.РасшифровкаКонтрагента = Новый Структура("Организация, Контрагент, ДатаНач, ДатаКон",
				Организация, Выборка.Контрагент, НачалоМесяца(Выборка2.Период), КонецМесяца(Выборка2.Период));
			
					
			Область.Область("R1C1:R1C3").ЦветФона  = ФормулаЦвета(сч);
			
			
			ТабРезультат.Присоединить(Область);
			
			СтрТотал = ТЗПериодов.Найти(Выборка2.Период, "Период");
			
			Попытка
				СтрТотал.ОбъемПродажТотал = СтрТотал.ОбъемПродажТотал + Выборка2.ОбъемПродаж;
			Исключение
			КонецПопытки;
			
			Попытка
				СтрТотал.СебестоимостьТотал = СтрТотал.СебестоимостьТотал + Выборка2.Себестоимость;
			Исключение
			КонецПопытки;
			
			сч=сч+1;
			
		КонецЦикла;
		
		
	КонецЦикла;
	
	
	
	//подвал
	
	Область = Макет.ПолучитьОбласть("Подвал|Начало");
	ТабРезультат.Вывести(Область);

	сч = 1;
	Для Каждого СтрокаПериод Из ТЗПериодов Цикл
		
		Область = Макет.ПолучитьОбласть("Подвал|Месяц");
		Область.Параметры.ОбъемПродаж = СтрокаПериод.ОбъемПродажТотал;
		Область.Параметры.Себестоимость = СтрокаПериод.СебестоимостьТотал;
		Область.Параметры.Рентабельность = ?(СтрокаПериод.СебестоимостьТотал=0, 0, (СтрокаПериод.ОбъемПродажТотал-СтрокаПериод.СебестоимостьТотал)/СтрокаПериод.СебестоимостьТотал*100);
		
		//расшифровка. аналог расшифровки месяца (пока)
		Область.Параметры.РасшифровкаИтого = Новый Структура("Организация, Контрагент, ДатаНач, ДатаКон",
			Организация, Неопределено, НачалоМесяца(СтрокаПериод.Период), КонецМесяца(СтрокаПериод.Период));
		
				
		Область.Область("R1C1:R1C3").ЦветФона  = ФормулаЦвета(сч);
		ТабРезультат.Присоединить(Область);
		
		сч=сч+1;
	КонецЦикла;
	
	
	
	
	//завершение
	ТабРезультат.Защита = Истина;
	
	ТабРезультат.ФиксацияСлева=2;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	СформироватьОтчетНаСервере();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ФормулаЦвета(сч)
	//доделать. цвет потом наверно нужно сделать фиксированный для каждого месяца
	Возврат Новый Цвет(120+сч*10, 120+сч*25, 110)
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
КонецПроцедуры


&НаКлиенте
Процедура ТабРезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	//если эта форма уже открыта в режиме расшифровки, то эта функция должна отрабатывать стандартно
	
	Если Параметры.КлючНазначенияИспользования = "ДляРасшифровки" Тогда
		СтандартнаяОбработка = Истина;
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка=Ложь;
	Расшифровка.Вставить("КлючНазначенияИспользования", "ДляРасшифровки");
	//нужно понять, внешяя это обработка или в составе метаданых
	
	Если ЭтоВнешнийОтчет() Тогда
		ОткрытьФорму("ВнешнийОтчет.ОбщийОтчетПоПродажамYTD.Форма.ФормаОтчета", Расшифровка)
	Иначе
		ОткрытьФорму("Отчет.ОбщийОтчетПоПродажамYTD.Форма.ФормаОтчета", Расшифровка)
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоВнешнийОтчет()
	
	//нужно понять, внешяя это обработка или в составе метаданых
	Попытка
		
		ПредставлениеОтчета = Строка(РеквизитФормыВЗначение("Отчет"));
		Если Найти(ПредставлениеОтчета, "Внешний")>0 Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.КлючНазначенияИспользования = "ДляРасшифровки" Тогда
		Параметры.Свойство("Контрагент", РКонтрагент);
		Параметры.Свойство("Организация", РОрганизация);
		Параметры.Свойство("ДатаНач", РДатаНач);
		Параметры.Свойство("ДатаКон", РДатаКон);
		
		ЭтаФорма.Заголовок = "Расшифровка";
		
		Элементы.Организация.Доступность = ЛОЖЬ;
		Элементы.Контрагент.Доступность = ЛОЖЬ;
		Элементы.НаДату.Доступность = ЛОЖЬ;
		Элементы.ФормаСформироватьОтчет.Доступность = Ложь;
		СформироватьРасшифровкуНаСервере();
		
	КонецЕсли;
КонецПроцедуры
